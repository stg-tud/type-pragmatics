module generate

imports
  libstratego-lib
  libstratego-gpp
  libstratego-aterm
  include/Veritas
  fof-rewriting
  smt-rewriting
  lib/editor-common.generated
  lib/runtime/index/core
  lib/runtime/task/core


rules // Incremental code generation of project using compilation library.
      
  // See lib/compilation-library.generated.str for documentation

  generate-files(|base, rulenames) =
  	?files; 
  	names := <generate-file-names(|base)> rulenames; 
  	<zip> (names, files);
  	map(write-file);
  	!names

  
  generate-file-names(|base) : [] -> []
  generate-file-names(|base) : [rule | rest] -> [outfile | <generate-file-names(|base)> rest]
  where
    <get-extension> base => ext;
    <remove-extension> base => basename;
    <conc-strings> (basename, "-", rule) => filename;
    <add-extension> (filename, ext) => outfile
  
  
  write-file : (file, output) -> <id>
  where
    <fopen> (file, "w") => stream;
    <fputs> (output, stream);
    <fclose> stream
    
  generate-fof :
  	   (selected, position, ast, path, project-path) -> result
  with
	index-setup(|<language>, project-path);
	task-setup(|project-path);
    filename := <guarantee-extension(|"fof")> path;
	rulez := <collect-all(?Goals(<filter(?TypingRule(_,RuleName(_,<id>),_))>) 
		  <+ ?GoalsWithStrategy(_,<filter(?TypingRule(_,RuleName(_,<id>),_))>)
	      <+ ?Lemmas(<filter(?TypingRule(_,RuleName(_,<id>),_))>)
	      <+ ?LemmasWithStrategy(_,<filter(?TypingRule(_,RuleName(_,<id>),_))>)); concat> ast; 
    goals := <fof-trans <+ error(!"fof-trans failed: ")> ast;
    strings := <map(pp-formula <+ pp-debug(pp-formula))> goals;
    outfiles := <generate-files(|filename,rulez)> strings;
    result := <if <?[]> goals then !None() else !(<Hd> outfiles, <Hd> strings) end> ()

  pp-formula =
    ast2abox(|[<parse-pptable-file> "trans/fof-rewriting.pp"]);
    box2text-string(|100)
  
  pp-debug(pp) = bottomup(is-string <+ is-list <+ pp <+ debug(!"pp failed: "))

  generate-smt :
  	   (selected, position, ast, path, project-path) -> (filename, result)
  with
   	index-setup(|<language>, project-path);
	task-setup(|project-path);
	filename := <guarantee-extension(|"smt2")> path;
    formula := <smt-trans> ast;
    result := <pp-smt> formula
    
  error(s) = debug(s);fail