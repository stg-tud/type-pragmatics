module runtime/analysis/reanalysis

imports
  
  runtime/analysis/core
  runtime/task/core
  runtime/index/core
  runtime/nabl/collect
  runtime/nabl/namegen
  runtime/properties/collect

rules
  
  start-reanalysis(|language, path, project-path) =
    nabl-namegen-clear(|<analysis-partition(|project-path)> path);
    index-push;
    task-push
    
  stop-reanalysis(|language, path, project-path) =
    task-pop;
    index-pop
  
  reanalysis(s|language, path, project-path) =
    where(start-reanalysis(|language, path, project-path); s; stop-reanalysis(|language, path, project-path))
  
  reanalyze-ast(|language, path, project-path):
    ast -> analyzed-ast
    with
      property-kind* := <nabl-get-all-properties>
    ; ast' := <topdown(try(remove-property-annotations(|property-kind*)))> ast
    ; partition := <analysis-partition(|project-path)> path
    ; Result([FileResult(_, _, _, _, analyzed-ast, error*, warning*, note*)], _, debug-result, _) := 
        <analysis-files-no-builtins(id, nabl-collect, id, id, fail, fail|language, project-path)> [File(partition, ast, 0.0)]
    ; <analysis-debug-results> debug-result  
    ; <map(analysis-debug(!"Error: "))> error*
    ; <map(analysis-debug(!"Warning: "))> warning*
    ; <map(analysis-debug(!"Note: "))> note*

