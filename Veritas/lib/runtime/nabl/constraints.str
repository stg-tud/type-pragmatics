module runtime/nabl/constraints

imports
  runtime/nabl/uri
  runtime/nabl/collect
  runtime/nabl/entries
  runtime/nabl/tasks
  runtime/task/core
  runtime/task/tasks
  runtime/properties/tasks
  
rules
  
  nabl-lookup-local(|ctx) =
    nabl-uri => uri
  ; nabl-uri-all-local-scopes
  ; nabl-resolve-task(|ctx, <nabl-uri-namespace> uri, <nabl-uri-name> uri, [])
  
  nabl-lookup-local(|ctx, ns) =
    nabl-uri => uri
  ; nabl-uri-all-local-scopes
  ; nabl-resolve-task(|ctx, ns, <nabl-uri-name> uri, [])
    
 nabl-lookup-local(|ctx, ns, name) =
    nabl-uri-all-local-scopes
  ; nabl-resolve-task(|ctx, ns, name, [])

rules
  
  nabl-lookup-lexical(|ctx) =
    nabl-uri => uri
  ; nabl-uri-all-lexical-scopes
  ; nabl-resolve-task(|ctx, <nabl-uri-namespace> uri, <nabl-uri-name> uri, [])
  
  nabl-lookup-lexical(|ctx, ns) =
    nabl-uri => uri
  ; nabl-uri-all-lexical-scopes
  ; nabl-resolve-task(|ctx, ns, <nabl-uri-name> uri, [])
    
 nabl-lookup-lexical(|ctx, ns, name) =
    nabl-uri-all-lexical-scopes
  ; nabl-resolve-task(|ctx, ns, name, [])
  
  nabl-lookup-lexical-parent(|ctx) =
    nabl-uri => uri
  ; nabl-uri-all-lexical-parent-scopes
  ; nabl-resolve-task(|ctx, <nabl-uri-namespace> uri, <nabl-uri-name> uri, [])
  
  nabl-lookup-lexical-parent(|ctx, ns) =
    nabl-uri => uri
  ; nabl-uri-all-lexical-parent-scopes
  ; nabl-resolve-task(|ctx, ns, <nabl-uri-name> uri, [])
    
 nabl-lookup-lexical-parent(|ctx, ns, name) =
    nabl-uri-all-lexical-parent-scopes
  ; nabl-resolve-task(|ctx, ns, name, [])
    
rules
  
  nabl-lookup-local-import(|ctx) =
    nabl-uri
  ; nabl-lookup-local-import(|ctx, <nabl-uri-namespace>, <nabl-uri-name>)
  
  nabl-lookup-local-import(|ctx, ns) =
    nabl-uri
  ; nabl-lookup-local-import(|ctx, ns, <nabl-uri-name>)
  
  nabl-lookup-local-import(|ctx, ns, name) =
    nabl-uri-parent
  ; prop-create-lookup(|ctx, Import(ns)); task-create-insert(|ctx); task-create-combine(|ctx)
  ; nabl-create-resolvedefs(|ctx, ns, name, [])
  
rules
  
  nabl-lookup-lexical-import(|ctx) =
    nabl-uri
  ; nabl-lookup-lexical-import(|ctx, <nabl-uri-namespace>, <nabl-uri-name>)
  
  nabl-lookup-lexical-import(|ctx, ns) =
    nabl-uri
  ; nabl-lookup-lexical-import(|ctx, ns, <nabl-uri-name>)
  
  nabl-lookup-lexical-import(|ctx, ns, name) =
    nabl-uri-parents
  ; map(
      prop-create-lookup(|ctx, Import(ns)); task-create-insert(|ctx); task-create-combine(|ctx)
    ; nabl-create-resolvedefs(|ctx, ns, name, [])
    )
  ; task-create-choice(|ctx)
    
